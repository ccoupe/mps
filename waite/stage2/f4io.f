C***********************************************************************
C 
C  TO PREPARE AND TEST THE DISTRIBUTED VERSION OF THE MOBILE PROGRAMMING
C  SYSTEM, FOLLOW THE PROCEDURE OUTLINED BELOW.  IN CASE OF DIFFICULTY, 
C  CONSULT THE IMPLEMENTATION GUIDE.
C 
C         1)  WRITE A FUNCTION SUBPROGRAM WHICH TESTS FOR END-OF-FILE 
C           (EOF) AFTER A READ OPERATION. 
C 
C              FUNCTION IFEOF(N,JBA)
C              DIMENSION JBA(80)
C 
C                   N        IS THE FORTRAN UNIT ON WHICH THE READ
C                            OPERATION TOOK PLACE.
C                   JBA      IS THE CARD IMAGE READ, IN A1 FORMAT.
C                   SET IFEOF=0 IF NO EOF WAS DETECTED. 
C                   SET IFEOF=NONZERO IF EOF WAS DETECTED.
C 
C              NOTE - IF YOUR VERSION OF FORTRAN INCORPORATES THE EOF 
C              TEST IN THE READ STATEMENT, MAKE APPROPRIATE CHANGES IN
C              THE IREAD AND ICLOS SUBROUTINES AND DISREGARD IFEOF. 
C 
C         2)  WRITE A FUNCTION SUBPROGRAM WHICH CONVERTS AN A1 FORMAT 
C           CHARACTER TO A NON-NEGATIVE INTEGER.  THE ONLY RESTRICTION
C           ON THIS CONVERSION IS THAT THE CHARACTERS 0-9 MUST BE 
C           CONVERTED INTO SUCCESSIVE INTEGERS. 
C 
C              FUNCTION ICVCI(JCHAR)
C 
C                   JCHAR    IS A CHARACTER IN A1 FORMAT. 
C                   SET ICVCI=THE NON-NEGATIVE INTEGER CORRESPONDING TO 
C                            THE CHARACTER. 
C 
C         3)  WRITE A FUNCTION SUBPROGRAM WHICH CONVERTS A NON-NEGATIVE 
C           INTEGER INTO A CHARACTER IN A1 FORMAT.  THE CONVERSION MUST 
C           BE THE INVERSE OF THAT PERFORMED BY ICVCI (SEE (2), ABOVE). 
C 
C              FUNCTION ICVIC(INT)
C 
C                   INT      IS A NON-NEGATIVE INTEGER. 
C                   SET ICVIC=THE CHARACTER, IN A1 FORMAT, CORRESPONDING
C                            TO THE INTEGER.  DO NOT ALTER INT. 
C 
C         4)  THE FORTRAN DECKS WHOSE SERIAL NUMBERS BEGIN WITH F4IO, 
C           TOGETHER WITH THE FUNCTION SUBPROGRAMS LISTED ABOVE, MAKE UP
C           THE I/O PACKAGE.  THE DECK SERIALIZED FIOT IS A SUBROUTINE
C           WHICH PERFORMS BASIC TESTS ON THE PACKAGE, USING THE DATA 
C           CARDS WHOSE SERIAL NUMBERS BEGIN WITH IOTD.  YOU SHOULD 
C           ENSURE THAT THE TEST PROGRAM WORKS BEFORE PROCEEDING FURTHER
C           (RUN 1, BELOW). 
C 
C         5)  MAKE THE REMAINING RUNS SHOWN IN THE TABLE BELOW.  RUNS 
C           3 AND 3A SHOULD BE MADE IN A SINGLE JOB, IF POSSIBLE, WITH
C           THE PUNCH OUTPUT OF 3 GOING TO AN INTERMEDIATE TAPE OR DISC 
C           FILE FOR INPUT TO RUN 3A.  RUNS 4, 4A AND 5, 5A SHOULD BE 
C           TREATED SIMILARLY.
C 
C         6)  THE OBJECT DECK RESULTING FROM RUN 5A IS THE EXECUTABLE 
C           VERSION OF STAGE2.
C 
C                            - - TABLE - -
C 
C-----------------------------------------------------------------------
C 
C  RUN  PROGRAM DECKS  DATA DECKS  OUTPUT  HOW OUTPUT IS USED 
C 
C-----------------------------------------------------------------------
C 
C  1    IFEOF          IOTD        PRINT   CHECK FOR ERRORS IN FORTRAN
C       ICVCI                      PUNCH   I/O. 
C       ICVIC 
C       F4IO
C       FIOT
C 
C-----------------------------------------------------------------------
C 
C  2    IFEOF          SIMT        PUNCH   CHECK FOR ERRORS IN SIMCMP OR
C       ICVCI                              FORTRAN I/O. 
C       ICVIC 
C       F4IO
C       SIMC
C 
C-----------------------------------------------------------------------
C 
C  3    IFEOF          FLBM        PUNCH   INPUT TO RUN 3A. 
C       ICVCI          FLT1 
C       ICVIC 
C       F4IO
C       SIMC
C 
C-----------------------------------------------------------------------
C 
C  3A   IFEOF          FLD1        PRINT   CHECK FOR MACRO CODING 
C       ICVCI                              ERRORS.
C       ICVIC 
C       F4IO
C       IWCH
C       OUTPUT OF RUN 3 
C 
C-----------------------------------------------------------------------
C 
C  4    IFEOF          FLBM        PUNCH   INPUT TO RUN 4A. 
C       ICVCI          FLT2 
C       ICVIC 
C       F4IO
C       SIMC
C 
C-----------------------------------------------------------------------
C 
C  4A   IFEOF          FLD2        PRINT   CHECK FOR MACRO CODING 
C       ICVCI                      PUNCH   ERRORS.
C       ICVIC 
C       F4IO
C       IWCH
C       OUTPUT OF RUN 4 
C 
C-----------------------------------------------------------------------
C 
C  5    IFEOF          FLBM        PUNCH   INPUT TO RUN 5A
C       ICVCI          STG2 
C       ICVIC 
C       F4IO
C       SIMC
C 
C-----------------------------------------------------------------------
C 
C  5A   IFEOF          ST2T        PRINT   CHECK FOR ERRORS IN STAGE2.
C       ICVCI                      PUNCH
C       ICVIC 
C       F4IO
C       IWCH
C       OUTPUT OF RUN 5 
C 
C***********************************************************************
C 
C  THE MAIN PROGRAM SETS UP THE CHANNEL DEFINITIONS AND ENSURES THAT
C  ALL CHANNELS ARE CLOSED AT THE END OF THE RUN.  EACH CHANNEL 
C  DEFINITION HAS THE FORM -
C 
C         JCHAN(KCH+1) = READ ACCESS CODE 
C         JCHAN(KCH+2) = WRITE ACCESS CODE
C         JCHAN(KCH+3) = OPEN ACCESS CODE 
C         JCHAN(KCH+4) = CONTROL ACCESS CODE
C         JCHAN(KCH+5) = FORTRAN UNIT NUMBER
C         JCHAN(KCH+6) = INITIAL CHANNEL STATUS 
C         KCH=KCH+6 
C 
C  THE ACCESS CODES ARE NON-NEGATIVE INTEGERS, AND THERE IS NO LIMIT TO 
C  THE NUMBER WHICH COULD BE PROVIDED.  THE DISTRIBUTED VERSION 
C  RECOGNIZES THE FOLLOWING - 
C 
C         READ      0 - READING IS ILLEGAL
C                   1 - READ ONE LINE, CONVERTING EACH CHARACTER TO 
C                       AN INTEGER AND STORING IT IN THE ARRAY
C                       SUPPLIED BY THE CALLER.  THE LINE IS ASSUMED TO 
C                       BE 80 CHARACTERS LONG.
C 
C         WRITE     0 - WRITING IS ILLEGAL
C                   1 - WRITE AN INTEGER ARRAY, CONVERTING EACH 
C                       ELEMENT TO A CHARACTER.  THE LINE IS ASSUMED TO 
C                       BE A FIXED LENGTH OF 80 CHARACTERS, AND IS
C                       FILLED OUT TO THIS NUMBER WITH SPACES IF
C                       NECESSARY.
C                   2 - WRITE AN INTEGER ARRAY, CONVERTING EACH ELEMENT 
C                       TO A CHARACTER.  THE LINE IS VARIABLE LENGTH, 
C                       AND THE USASI CARRIAGE CONTROL CHARACTER FOR
C                       SINGLE SPACING IS ATTACHED TO THE FRONT OF THE
C                       LINE. 
C                   3 - AS FOR (2), EXCEPT THAT TOP-OF-PAGE CARRIAGE
C                       CONTROL IS USED.  THE WRITE ROUTINE RESETS THIS 
C                       ACCESS CODE TO 2 AFTER THE LINE IS OUTPUT.
C 
C         OPEN      0 - NO ACTION IS REQUIRED TO OPEN THE DEVICE
C                   1 - THE DEVICE IS REWOUND EACH TIME IT IS OPENED
C 
C         CONTROL   0 - NO CONTROL OPERATIONS ARE AVAILABLE 
C                   1 - IF THE DEVICE IS OPEN FOR READING AND HAS NOT 
C                       REACHED END-OF-FILE, THEN SUCCESSIVE READS ARE
C                       PERFORMED UNTIL END-OF-FILE IS DETECTED.
C                   2 - IF THE DEVICE IS OPEN FOR WRITING, AND HAS NOT
C                       REACHED END-OF-MEDIUM, THEN A FILE MARK IS
C                       WRITTEN ON THE DEVICE.
C 
C  THE CHANNEL STATUS SPECIFIES THE CURRENT CONDITION OF THE CHANNEL. 
C  IT IS GENERALLY INITIALIZED TO 1.
C 
C         0 - THE CHANNEL IS NOT AVAILABLE
C         1 - THE CHANNEL IS CLOSED 
C         2 - THE CHANNEL IS OPEN FOR READING 
C         3 - THE CHANNEL IS OPEN FOR WRITING 
C         4 - THE CHANNEL IS OPEN FOR BOTH READING AND WRITING
C         5 - AN END-OF-FILE OR END-OF-MEDIUM HAS BEEN DETECTED 
C 
C  THE DISTRIBUTED VERSION HAS FOUR CHANNELS.  TO CHANGE THIS NUMBER, 
C  YOU MUST CHANGE THE DIMENSION OF JCHAN AND ADD OR DELETE CHANNEL 
C  DEFINITIONS.  JCHAN MUST BE DIMENSIONED AT SIX TIMES THE NUMBER OF 
C  CHANNELS, AND CHANNEL DEFINITIONS SHOULD ALWAYS BE PLACED BETWEEN
C  KCH = 0 AND THE ASSIGNMENT TO MAXCH.  THIS ENSURES THAT MAXCH IS 
C  CORRECTLY SET. 
C 
      DIMENSION JCHAN(24) 
      COMMON MAXCH,JCHAN
      KCH=0 
C 
C         DEFINE CHANNEL 1
C 
      JCHAN(KCH+1)=1
      JCHAN(KCH+2)=0
      JCHAN(KCH+3)=0
      JCHAN(KCH+4)=1
      JCHAN(KCH+5)=5
      JCHAN(KCH+6)=1
      KCH=KCH+6 
C 
C         DEFINE CHANNEL 2
C 
      JCHAN(KCH+1)=1
      JCHAN(KCH+2)=1
      JCHAN(KCH+3)=1
      JCHAN(KCH+4)=2
      JCHAN(KCH+5)=4
      JCHAN(KCH+6)=1
      KCH=KCH+6 
      OPEN (4,FILE='CHANNEL2.DAT')
C 
C         DEFINE CHANNEL 3
C 
      JCHAN(KCH+1)=0
      JCHAN(KCH+2)=1
      JCHAN(KCH+3)=0
      JCHAN(KCH+4)=2
      JCHAN(KCH+5)=7
      JCHAN(KCH+6)=1
      KCH=KCH+6 
      OPEN (7,FILE='CHANNEL3.DAT')
C 
C         DEFINE CHANNEL 4
C 
      JCHAN(KCH+1)=0
      JCHAN(KCH+2)=3
      JCHAN(KCH+3)=0
      JCHAN(KCH+4)=0
      JCHAN(KCH+5)=6
      JCHAN(KCH+6)=1
      KCH=KCH+6 
C 
C         SET THE NUMBER OF CHANNELS AVAILABLE. 
C 
      MAXCH=KCH/6 
C 
C         CALL THE USER PROGRAM.  ALL USER PROGRAMS ARE COMPILED AS 
C         SUBROUTINES WITH THE NAME PROGR.  THE ACTUAL PROGRAM MIGHT
C         BE SIMCMP, ONE OF THE TEST PROGRAMS OR STAGE2.
C 
      CALL PROGR
C 
C         CLOSE ALL FILES.  NOTE THAT ONLY THOSE WHICH ARE ACTUALLY 
C         OPEN WILL BE CLOSED.  THE OTHERS WILL RETURN ERROR INDICATIONS
C         WHICH ARE IGNORED.
C 
      DO 1 K=1,MAXCH
    1 KCH=IOOP(0,K,JCHAN,1,1) 
C 
C         TERMINATE EXECUTION NORMALLY. 
C 
      STOP
C 
      END 
      FUNCTION IOOP(JOP,JCH,JBA,JP1,JP2)
C 
C  IOOP HANDLES ALL OF THE ERROR CHECKING AND SUPERVISION OF I/O FOR THE
C  SYSTEM.  IT IS CALLED WITH THE OPERATION CODE, CHANNEL NUMBER AND
C  BUFFER ADDRESS.  AFTER CHECKING THE CHANNEL NUMBER FOR LEGALITY, IT
C  EXAMINES THE STATUS OF THE CHANNEL AND SELECTS THE PROPER ROUTINES 
C  TO HANDLE THE I/O REQUESTED.  IF IT DETECTS ANY INCONSISTENCIES, THEN
C  IT RETURNS AN ILLEGAL OPERATION INDICATION.
C 
C         JOP  IS THE OPERATION CODE. 
C 
C         JCH  IS THE CHANNEL NUMBER, IN THE RANGE 0-MAXCH INCLUSIVE. 
C 
C         JBA  IS THE BASE ADDRESS OF THE USER ARRAY. 
C 
C         JP1 AND JP2 ARE POINTERS ASSOCIATED WITH THE USER ARRAY.
C 
C  THE VALUE OF IOOP REFLECTS THE COMPLETION OF THE OPERATION-
C 
C         IOOP = 0  OPERATION COMPLETED NORMALLY
C                1  END-OF-FILE OR END-OF-MEDIUM WAS DETECTED 
C                2  THE OPERATION REQUESTED IS ILLEGAL ON THE SPECIFIED 
C                        CHANNEL
C                3 AND HIGHER  THERE WAS AN ERROR.  THE VALUE OF IOOP 
C                        IS THE ERROR CODE. 
C 
      DIMENSION JCHAN(24) 
      DIMENSION JBA(1)
      COMMON MAXCH,JCHAN
C 
C         CHECK THE LEGALITY OF THE CHANNEL NUMBER.  (CHANNEL 0 IS
C         HANDLED SPECIALLY.) 
C 
      IF (JCH)  7,5,1 
    1 IF (MAXCH .LT. JCH)  GO TO 7
C 
C         COMPUTE THE BASE ADDRESS OF THE STATUS WORDS FOR THIS CHANNEL.
C 
      KCH=(JCH-1)*6 
C 
C         PREPARE FOR COMPUTED GO-TO ON THE CHANNEL STATUS. 
C 
      K=JCHAN(KCH+6)
      IF (K .EQ. 0)  GO TO 7
C 
C         JUMP ON THE TYPE OF OPERATION.
C 
      IF (JOP)  10,2,20 
C 
C         ***** PROCESS CONTROL REQUESTS *****
C 
C         CONTROL OPERATIONS ARE ILLEGAL ON INACTIVE DEVICES
C 
    2 IF (JCHAN(KCH+6) .EQ. 1)  GO TO 7 
C 
C         IF ANY CONTROL OPERATIONS ARE AVAILABLE, CALL ON THE CONTROL
C         SUBROUTINE. 
C 
      IF (JCHAN(KCH+4) .EQ. 0)  GO TO 3 
      CALL ICNTL(KCH,JBA,JP1,JP2,IOOP)
      RETURN
C 
C         OTHERWISE MAKE CERTAIN THAT THE REQUEST IS A CLOSE, AND THEN
C         SET THE DEVICE INACTIVE.
C 
    3 IF (JP1 .NE. JP2)  GO TO 7
      JCHAN(KCH+6)=1
C 
C         COMMON RETURN FOR SUCCESS 
C 
    4 IOOP=0
      RETURN
C 
C         A READ ON CHANNEL 0 RETURNS ENDFILE, A WRITE OR CLOSE IS
C         IGNORED.
C 
    5 IF (JOP .GE. 0)  GO TO 4
C 
C         COMMON RETURN FOR END-OF-FILE INDICATIONS.
C 
    6 IOOP=1
      RETURN
C 
C         COMMON RETURN FOR ILLEGAL OPERATIONS
C 
    7 IOOP=2
      RETURN
C 
C         ***** PROCESS READ REQUESTS ***** 
C 
C         CHECK THE STATUS OF THE CHANNEL AND ACT ACCORDINGLY.
C 
   10 GO TO (11,12,7,12,6),K
C 
C         IF READING IS NOT POSSIBLE, THEN RETURN AN ILLEGAL OPERATION
C 
   11 IF (JCHAN(KCH+1) .EQ. 0)  GO TO 7 
C 
C         SET THE CHANNEL STATUS
C 
      JCHAN(KCH+6)=2
C 
C         IF ANY ACTION IS REQUIRED TO OPEN THE DEVICE, CALL SUBROUTINE 
C 
      IF (JCHAN(KCH+3) .EQ. 0)  GO TO 12
      CALL IOPEN(-JOP,KCH,JBA,JP1,JP2,IOOP) 
      IF (IOOP .NE. 0)  RETURN
C 
C         TRANSMIT ONE RECORD 
C 
   12 CALL IREAD(-JOP,KCH,JBA,JP1,JP2,IOOP) 
      GO TO 30
C 
C         ***** PROCESS WRITE REQUESTS *****
C 
C         CHECK THE STATUS OF THE CHANNEL AND ACT ACCORDINGLY.
C 
   20 GO TO (21,7,22,22,6),K
C 
C         IF WRITING IS NOT POSSIBLE, THEN RETURN AN ILLEGAL OPERATION
C 
   21 IF (JCHAN(KCH+2) .EQ. 0)  GO TO 7 
C 
C         SET THE CHANNEL STATUS
C 
      JCHAN(KCH+6)=3
C 
C         IF ANY ACTION IS REQUIRED TO OPEN THE DEVICE, CALL SUBROUTINE 
C 
      IF (JCHAN(KCH+3) .EQ. 0)  GO TO 22
      CALL IOPEN(JOP,KCH,JBA,JP1,JP2,IOOP)
      IF (IOOP .NE. 0)  RETURN
C 
C         TRANSMIT ONE RECORD 
C 
   22 CALL IWRIT(JOP,KCH,JBA,JP1,JP2,IOOP)
C 
C         IF END-OF-FILE OR END-OF-MEDIUM WAS DETECTED, SET THE CHANNEL 
C         STATUS ACCORDINGLY. 
C 
   30 IF (IOOP .EQ. 1)  JCHAN(KCH+6)=5
      RETURN
C 
      END 
      SUBROUTINE IREAD(JOP,KCH,JBA,JP1,JP2,JRES)
C 
C  THIS SUBROUTINE ACTUALLY PERFORMS A READ OPERATION.
C 
C         JOP  IS THE OPERATION CODE. 
C 
C         KCH  IS THE INDEX TO THE CHANNEL DEFINITION IN JCHAN. 
C 
C         JBA  IS THE BASE ADDRESS OF THE USER ARRAY. 
C 
C         JP1   IS THE INDEX OF THE FIRST AVAILABLE SPACE IN THE ARRAY
C 
C         JP2   IS TO BE SET TO INDEX THE FIRST FREE WORD BEYOND THE
C                   CHARACTERS READ IN
C 
C         JRES  IS TO BE SET TO REFLECT THE COMPLETION OF THE OPERATION 
C 
C                   0 - THE OPERATION WAS COMPLETED NORMALLY
C                   1 - AN END-OF-FILE WAS DETECTED 
C 
      CHARACTER*80 LINE
      INTEGER JBA(12000),JCHAN(24)
      COMMON MAXCH,JCHAN
C 
C         SPECIFY THE LENGTH OF THE INPUT LINES.
C 
      DATA LNLNG/80/
C 
C         ASSUME NORMAL COMPLETION
C 
      JRES=0
C 
C         PICK UP THE FORTRAN LOGICAL UNIT NUMBER.
C 
      JDEV=JCHAN(KCH+5) 
C 
C         READ ONE CARD INTO THE USERS BUFFER IN A1 FORMAT AND CHECK
C         FOR END-OF-FILE.
C 
      READ (JDEV,100,END=2) LINE
C 
C         CONVERT FROM A1 FORMAT TO INTEGERS, USING THE MAPPING 
C         PROVIDED BY THE USER. 
C 
      JP2=JP1 
      DO 1 K=1,LNLNG
      JBA(JP2)=ICHAR(LINE(K:K))
    1 JP2=JP2+1
C 
      RETURN
C 
C         RETURN THE ENDFILE INDICATION TO IOOP 
C 
    2 JRES=1
      RETURN
C 
  100 FORMAT (A) 
C 
      END 
      SUBROUTINE IWRIT(JOP,KCH,JBA,JP1,JP2,JRES)
C 
C  THIS SUBROUTINE ACTUALLY PERFORMS A WRITE OPERATION. 
C 
C         JOP  IS THE OPERATION CODE. 
C 
C         KCH  IS THE INDEX TO THE CHANNEL DEFINITION IN JCHAN. 
C 
C         JBA  IS THE BASE ADDRESS OF THE USER ARRAY. 
C 
C         JP1   IS THE INDEX OF THE FIRST CHARACTER TO BE WRITTEN FROM
C                   THE ARRAY 
C 
C         JP2   IS THE INDEX OF THE FIRST WORD BEYOND THE CHARACTERS TO 
C                   BE WRITTEN
C 
C         JRES  IS TO BE SET TO REFLECT THE COMPLETION OF THE OPERATION 
C 
C                   0 - THE OPERATION WAS COMPLETED NORMALLY
C                   1 - AN END-OF-MEDIUM PREVENTED THE WRITE
C 
      CHARACTER*119 LINE
      INTEGER JBA(12000),JCHAN(24)
      COMMON MAXCH,JCHAN
C 
C         DEFINE THE LENGTHS OF THE MACHINE- AND HUMAN-READABLE LINES.
C 
      DATA LNLNG,KMAX/80,119/ 
C
C         SILLY CONSTANT TO FOOL THE WICAT
C
      DATA IZERO/0/
C 
C         ASSUME NORMAL COMPLETION
C 
      JRES=0
C 
C         PICK UP THE ACCESS CODE AND FORTRAN LOGICAL UNIT NUMBER 
C 
      JHOW=JCHAN(KCH+2) 
      JDEV=JCHAN(KCH+5) 
C 
C         THE USER MAY REQUEST A BLANK LINE BY SPECIFYING NO CHARACTERS 
C 
      IF (JP1 .EQ. JP2)  GO TO 2
C 
C         OTHERWISE CONVERT THE INTEGERS IN THE USERS ARRAY TO
C         CHARACTERS, SAVING THEM IN THE TEMPORARY BUFFER.  NOTE HERE 
C         THAT WE ARE GUARANTEED AT LEAST ONE CHARACTER.
C 
      K1=JP1
      DO 1 K=1,KMAX 
      LINE(K:K)=CHAR(JBA(K1)+IZERO)
      K1=K1+1 
      IF (K1 .EQ. JP2)  GO TO 3 
    1 CONTINUE
      K=KMAX
      GO TO 3 
C 
C         MAKE AN EMPTY LINE INTO A SINGLE SPACE. 
C 
    2 LINE=' '
      K=1 
C 
C         SELECT THE WRITE STATEMENT ACCORDING TO HOW THE DEVICE IS TO
C         BE ACCESSED.
C 
    3 GO TO (10,20,30),JHOW 
C 
C         ***** MACHINE-READABLE OUTPUT ***** 
C 
C              ASSUME - 
C 
C                   1)  FIXED-LENGTH LINES ARE TO BE WRITTEN. 
C                   2)  NO CARRIAGE CONTROL IS TO BE ADDED. 
C 
C         FILL OUT THE LINE TO LNLNG CHARACTERS IF NECCESARY. 
C 
   10 CONTINUE
C      IF (K .GE. LNLNG)  GO TO 12 
C      I=K+1 
C      DO 11 K=I,LNLNG 
C   11 LINE(K)=KFILL 
C   12 CONTINUE
      WRITE (JDEV,100) LINE(1:K)
      RETURN
C 
C         ***** HUMAN-READABLE OUTPUT ***** 
C 
C              ASSUME - 
C 
C                   1)  VARIABLE-LENGTH LINES.
C                   2)  ANSI CARRIAGE CONTROL CHARACTERS MUST BE ADDED.
C                   3)  THE DEVICE WILL ACCEPT LINES OF UP TO KMAX
C                        CHARACTERS.
C 
C         PRINT WITH SINGLE SPACING 
C 
   20 WRITE (JDEV,101) LINE(1:K)
      RETURN
C 
C         START A NEW PAGE. 
C 
   30 WRITE (JDEV,102) LINE(1:K)
C 
C         RESET THE ACCESS CONTROL TO AVOID MULTIPLE PAGE THROWS. 
C 
      JCHAN(KCH+2)=2
      RETURN
C 
C         FORMAT FOR A MACHINE-READABLE LINE
C 
  100 FORMAT (A)
C 
C         FORMAT FOR A SINGLE-SPACE PRINT LINE
C 
  101 FORMAT (1H ,A)
C 
C         FORMAT FOR A TOP-OF-FORM PRINT LINE 
C 
  102 FORMAT (1H1,A)
C 
      END 
      SUBROUTINE IOPEN(JOP,KCH,JBA,JP1,JP2,JRES)
C 
C  THIS SUBROUTINE PERFORMS ANY ACTION REQUIRED WHEN OPENING A FILE.
C 
C         JOP  IS THE OPERATION CODE. 
C 
C         KCH  IS THE INDEX TO THE CHANNEL DEFINITION IN JCHAN. 
C 
C         JBA  IS THE BASE ADDRESS OF THE USER ARRAY. 
C 
C         JP1 AND JP2 ARE POINTERS ASSOCIATED WITH THE USER ARRAY.
C 
C         JRES  IS SET TO REFLECT COMPLETION OF THE OPERATION 
C 
C  IN THE DISTRIBUTED SYSTEM, THE ONLY ACTION PROVIDED IS A REWIND. 
C 
      DIMENSION JBA(12000),JCHAN(24)
      COMMON MAXCH,JCHAN
C 
C         PICK UP THE FORTRAN LOGICAL UNIT NUMBER.
C 
      JDEV=JCHAN(KCH+5) 
C 
C 
      REWIND JDEV 
      JRES=0
      RETURN
C 
      END 
      SUBROUTINE ICNTL(KCH,JBA,JP1,JP2,JRES)
C 
C  THIS SUBROUTINE ACTUALLY PERFORMS A CONTROL OPERATION
C 
C         KCH  IS THE INDEX TO THE CHANNEL DEFINITION IN JCHAN. 
C 
C         JBA  IS THE BASE ADDRESS OF THE USER ARRAY. 
C 
C         JP1 AND JP2 ARE POINTERS ASSOCIATED WITH THE USER ARRAY.
C 
C         JRES  IS SET TO REFLECT COMPLETION OF THE OPERATION 
C 
C  IN THE DISTRIBUTED SYSTEM, ONLY CLOSE OPERATIONS ARE PROVIDED.  THE
C  VALUE OF THE CONTROL ACCESS CODE SELECTS THE ACTION -
C 
C         1 - IF THE FILE IS OPEN FOR READING, AND HAS NOT REACHED END- 
C             OF-FILE, THEN SUCCESSIVE READS ARE PERFORMED UNITL END-OF-
C             FILE IS DETECTED. 
C         2 - IF THE FILE IS OPEN FOR WRITING, AND HAS NOT REACHED END- 
C             OF-MEDIUM, THEN A FILE MARK IS WRITTEN ON THE FILE. 
C 
      CHARACTER*80 JTB
      INTEGER JBA(12000),JCHAN(24)
      COMMON MAXCH,JCHAN
C 
C         NO ACTION IS NECCESARY UNLESS THE FILE STATUS IS ONE GREATER
C         THAN THE ACCESS CODE FOR CLOSE OPERATIONS 
C 
      IF (JCHAN(KCH+4)+1 .NE. JCHAN(KCH+6))  GO TO 3
C 
C         PICK UP THE FORTRAN LOGICAL UNIT NUMBER 
C 
      JDEV=JCHAN(KCH+5) 
C 
C         SELECT THE PROPER ACTION ON THE BASIS OF THE ACCESS CODE
C 
      IF (JCHAN(KCH+4) .EQ. 2)  GO TO 2 
C 
C         READ LINES UNTIL END-OF-FILE IS DETECTED
C 
    1 READ (JDEV,100,END=3) JTB 
      GO TO 1 
C 
C         WRITE THE FILE MARK 
C 
    2 ENDFILE JDEV
    3 JCHAN(KCH+6)=1
      JRES=0
      RETURN
C 
C         FORMAT FOR READ TO ENDFILE
C 
  100 FORMAT (A) 
C 
      END 
