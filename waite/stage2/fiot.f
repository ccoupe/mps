      SUBROUTINE PROGR
C 
C  THIS ROUTINE MAKES SEVERAL BASIC TESTS ON THE DISTRIBUTED I/O
C  PACKAGE.  ALTHOUGH IT IS NOT EXHAUSTIVE BY ANY MEANS, IT DOES SEEM 
C  TO POINT UP THE MOST COMMON FAULTS.  YOU SHOULD CONSULT THE
C  IMPLEMENTATION GUIDE FOR AN EXPLANATION OF THE OUTPUT TO BE EXPECTED 
C  FROM THE TEST, AND FOR THE MEANING OF VARIOUS ERRORS.
C 
C  THE ASSUMED CHANNEL CONFIGURATION FOR THIS PROGRAM IS- 
C 
C         CHANNEL 1 - DATA INPUT (USE THE CARDS SERIALIZED IOTD)
C         CHANNEL 3 - MACHINE READABLE OUTPUT 
C         CHANNEL 4 - PRINTER OUTPUT
C 
      DIMENSION LIST(160) 
C 
C         READ A TITLE LINE AND WRITE IT ON BOTH OUTPUT CHANNELS
C 
      IF (IOOP(-1,1,LIST,1,I) .NE. 0)  STOP 11
      IF (IOOP(1,3,LIST,1,I) .NE. 0)  STOP 31 
C 
C         THE TITLE SPECIFIES CHANNEL 3.  BEFORE WRITING IT TO THE
C         PRINTER, CHANGE THE 3 TO A 4.  (REMEMBER THAT THE DIGIT 
C         CHARACTERS MAP INTO SUCCESSIVE INTEGERS.) 
C 
      LIST(46)=LIST(46)+1 
      IF (IOOP(1,4,LIST,1,I) .NE. 0)  STOP 41 
C 
C        FOLLOW THE TITLE WITH A BLANK LINE ON CHANNEL 4
C 
      IF (IOOP(1,4,LIST,1,1) .NE. 0)  STOP 42 
C 
C         READ AND PRINT THE FIRST LINE OF THE TEST.
C 
      IF (IOOP(-1,1,LIST,1,I) .NE. 0)  STOP 12
      IF (IOOP(1,4,LIST,1,I) .NE. 0)  STOP 43 
C 
C         BUMP THE LINE NUMBER AND PRINT ONLY IT. 
C 
      LIST(1)=LIST(1)+1 
      IF (IOOP(1,4,LIST,1,3) .NE. 0)  STOP 44 
C 
C         READ TWO LINES INTO DIFFERENT STORE AREAS, TO SEE IF THEY 
C         INTERACT IN ANY WAY.
C 
      IF (IOOP(-1,1,LIST,1,I) .NE. 0)  STOP 13
      IF (IOOP(-1,1,LIST,I,J) .NE. 0)  STOP 14
C 
C         PRINT FROM THE FIRST AREA, WHICH SHOULD BE THE FIRST LINE.
C 
      IF (IOOP(1,4,LIST,1,I) .NE. 0)  STOP 45 
C 
C         PRINT FROM THE SECOND AREA USING A BASE OFFSET INSTEAD OF AN
C         INDEX OFFSET.  THIS MAY BOTHER SOME SYSTEMS.  IT IS USED IN 
C         SIMCMP, SO IT MUST WORK PROPERLY.  CONSULT THE IMPLEMENTATION 
C         GUIDE IF IT DOES NOT. 
C 
      IF (IOOP(1,4,LIST(I),1,J-I+1) .NE. 0)  STOP 46
C 
C         CHECK TO ENSURE THAT AN ENDFILE IS CORRECTLY RECOGNIZED.
C 
      IF (IOOP(-1,1,LIST,I,J) .NE. 1)  STOP 15
      RETURN
C 
      END 
